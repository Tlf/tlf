# Process this file with autoconf to produce a configure script.
AC_INIT(src, 1.0.0pre3, rein@couperus.com es tb@forth-ev.de)
AM_INIT_AUTOMAKE(tlf, 1.0.0pre3)
AM_CONFIG_HEADER(config.h)

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_AWK

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h stdlib.h string.h sys/ioctl.h unistd.h])

# Checks for libraries.
AC_CHECK_LIB([m], [atan])
AC_CHECK_LIB([pthread], [pthread_create])
ACX_PTHREAD
AC_CHECK_LIB([ncurses], [initscr])

#dnl check for HAMLIB
#AC_CHECK_LIB(hamlib,main,,AC_MSG_ERROR([Hamradio control libraries (hamlib) not found! ABORTED!]))
#AC_CHECK_HEADERS(hamlib/rig.h hamlib/riglist.h,,AC_MSG_ERROR([hamlib header(s) not found! ABORTED]))

dnl Check if we want to link the Hamradio control libraries (hamlib)
AC_ARG_ENABLE([hamlib],
[  --enable-hamlib   Add support for hamradio control libraries],
[case "${enableval}" in
	yes) wanthamlib=true ;;
	no)  wanthamlib=false ;;
#	*)   AC_MSG_ERROR([[bad value ${enableval} for --enable-hamlib]]) ;;
	esac],
[wanthamlib=false])

if test "x${wanthamlib}" != "xfalse"; then
    dnl Look for hamlib
    hamlib_modules="hamlib >= 1.2.8"
    PKG_CHECK_MODULES(HAMLIB, [$hamlib_modules], [
                           CFLAGS="$CFLAGS $HAMLIB_CFLAGS";
                           LIBS="$LIBS $HAMLIB_LIBS";
                           ], [
                               AC_MSG_ERROR([Hamradio control libraries 1.2.8 or later not found...])
                           ])

	AC_CHECK_LIB(hamlib,main,,AC_MSG_ERROR([Hamradio control libraries not found...]))
	AC_CHECK_HEADERS(hamlib/rig.h,,AC_MSG_ERROR([Hamlib headers not found...]))
	AC_DEFINE([WANT_HAMLIB], 1, [Want Hamlib])
	echo "enabling hamlib support"
else
	AC_DEFINE([WANT_HAMLIB], 0, [No Hamlib])
	echo "disabling hamlib support"
fi

if test "${ac_cv_c_compiler_gnu}" = "yes"; then
	CFLAGS="${CFLAGS} -Wall"
fi



# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_TM

# Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS([floor sqrt strchr strcspn strspn strstr])

# Set PACKAGE_DATA_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR,"${ac_default_prefix}/share/${PACKAGE}",
		   [package_data_dir is in default])
else
AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${prefix}/share/${PACKAGE}",
		   [package_data_dir is in prefix])
fi


AC_OUTPUT([Makefile
	doc/Makefile
	rules/Makefile
	scripts/Makefile
	share/Makefile
	src/Makefile
	tlf.1])

